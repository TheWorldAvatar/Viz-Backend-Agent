PREFIX cmns-doc:          <https://www.omg.org/spec/Commons/Documents/>
PREFIX cmns-qtu:          <https://www.omg.org/spec/Commons/QuantitiesAndUnits/>
PREFIX dc-terms:          <http://purl.org/dc/terms/>
PREFIX fibo-fbc-pas-caa:  <https://spec.edmcouncil.org/fibo/ontology/FBC/ProductsAndServices/ClientsAndAccounts/>
PREFIX fibo-fnd-acc-cur:  <https://spec.edmcouncil.org/fibo/ontology/FND/Accounting/CurrencyAmount/>
PREFIX p2p-o-doc-line:    <https://purl.org/p2p-o/documentline#>
PREFIX p2p-o-inv:         <https://purl.org/p2p-o/invoice#>
PREFIX p2p-o-item:        <https://purl.org/p2p-o/item#>

SELECT DISTINCT ?discount ?charge ?description ?amount
WHERE {
  ?iri dc-terms:identifier "[target]" .
  ?invoice cmns-doc:isAbout ?iri ;
    p2p-o-inv:hasTotalAmountWithVAT/fibo-fnd-acc-cur:hasAmount ?amount .
  {
    ?invoice p2p-o-inv:hasInvoiceLine ?discountline .
    ?discountline p2p-o-doc-line:hasPriceDiscountOfItem/cmns-qtu:hasNumericValue ?discount ;
      p2p-o-doc-line:lineNote ?description .
  } UNION {
    ?invoice p2p-o-inv:hasInvoiceLine ?chargeline .
    ?chargeline p2p-o-doc-line:hasGrosspriceOfItem/cmns-qtu:hasNumericValue ?charge ;
      p2p-o-doc-line:lineNote ?description .
    OPTIONAL{?chargeline p2p-o-doc-line:lineIdentifier ?order_id.}
    BIND(COALESCE(?order_id, "9999") AS ?order )
  } UNION {
    ?invoice p2p-o-inv:hasInvoiceLine ?chargeline .
    ?chargeline p2p-o-doc-line:hasGrosspriceOfItem/cmns-qtu:hasNumericValue ?charge ;
      p2p-o-item:hasItem/p2p-o-item:itemDescription ?description .
    OPTIONAL{?chargeline p2p-o-doc-line:lineIdentifier ?order_id.}
    BIND(COALESCE(?order_id, "10000") AS ?order )
  }
}
ORDER BY ?order