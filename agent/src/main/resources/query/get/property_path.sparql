PREFIX sh:             <http://www.w3.org/ns/shacl#>
PREFIX fibo-fnd-dt-fd: <https://spec.edmcouncil.org/fibo/ontology/FND/DatesAndTimes/FinancialDates/>

SELECT DISTINCT ?clazz ?instance_clazz ?name ?nodegroup ?nested_class ?branch ?subject ?isarray ?isoptional ?isnumber ?multipath ?multipath_proppath
WHERE{
  ?shape rdf:type sh:NodeShape ;
         sh:targetClass ?clazz ;
         sh:targetClass [target] ;
         (sh:or/rdf:rest*/rdf:first)? ?node.
  OPTIONAL {
    ?node sh:name ?branch .
  }
  OPTIONAL {
    ?node sh:property[shape] ?property .
    ?property sh:name ?name ;
              sh:path ?predicatepath .
    OPTIONAL{?property sh:node/sh:targetClass ?nested_class}
    OPTIONAL{
      ?property sh:hasValue ?parentSubjectValue .
    }
    OPTIONAL{
      {?property sh:minCount ?minCount.} UNION {?property ^sh:group/sh:minCount ?minCount.}
    }
    OPTIONAL{
      {?property sh:maxCount ?maxCount.} UNION {?property ^sh:group/sh:maxCount ?maxCount.}
    }
    # Verify numerical value
    OPTIONAL{
      ?property sh:datatype ?datatype.
    }
    # Predicate paths may be stored in a list or not, and use of ? helps to extract the related property
    OPTIONAL{
      ?predicatepath rdf:first?[path] ?multipath_initial .
      # If the property path is a nested blank node, extract both the shacl path restriction eg sh:inversePath and path name
      OPTIONAL{
        ?multipath_initial ?multipath_proppath ?multipath_listitem .
        FILTER STRSTARTS(STR(?multipath_proppath), STR(sh:))
      }
      # Extract the required path variable
      BIND(IF(?multipath_proppath != "" && isBlank(?multipath_initial), ?multipath_listitem, ?multipath_initial) AS ?multipath)
      FILTER(BOUND(?multipath))
    }

    # Retrieve sub target value first; Else, attempt to retrieve from parent or return null 
    BIND(IF(BOUND(?parentSubjectValue), ?parentSubjectValue, ?null) AS ?subject)
    # Check if the path has a min count property to determine if it is optional
    BIND(BOUND(?minCount) && (?minCount=0) AS ?isoptional)
    # Determine if it is an array based on the presence of a max count or at least more than one
    BIND(!BOUND(?maxCount) || (?maxCount>1) AS ?isarray)
    # Verify if the data type is a number (integer/decimal)
    BIND(BOUND(?datatype) && (?datatype = xsd:integer || ?datatype =xsd:decimal) AS ?isnumber)
    
    # Retrieve results for non-regular schedules
    OPTIONAL{?property sh:class ?instance_clazz}
    FILTER(!BOUND(?instance_clazz) || ?instance_clazz != fibo-fnd-dt-fd:RegularSchedule)

    # Only retrieve results if there is a multipath
    FILTER(BOUND(?multipath))
  }

  [filter]
}